#!/usr/bin/env stap++

global robbers, total

probe scheduler.ctxswitch {
    my_pid = target()
    if (prev_pid == my_pid && next_pid != 0 && next_pid != my_pid) {
        name = pid2execname(next_pid)
        if (name == "") {
            name = sprintf("%d", next_pid)
        }
        robbers[name] <<< 1
        total++
    }
}

probe end {
    printf("\n")
    i = 0
    foreach (robber in robbers- limit $^arg_limit :default(20)) {
        i++
        cnt = @count(robbers[robber])
        printf("#%d %s: %d%% (%d samples)\n", i, robber, cnt * 100 / total, cnt)
    }
    if (i == 0) {
        printf("No CPU time robbers found so far.\n")
    }
}

probe begin {
    printf("Start tracing process %d ($^exec_path)...\n", target())
    %( "$^arg_time :default()" != ""  %?
    printf("Please wait for $^arg_time seconds...\n")
    %:
    printf("Hit Ctrl-C to end.\n")
    %)
}

%( "$^arg_time" != ""  %?
probe timer.s($^arg_time) {
    exit()
}
%)
